; Zielarstwo
; Autor: drm
; Modyfikacja: Niewiedza, krowa, Veis
; Changelog
; Krowa:
;  2017.11
;  * Modyfikacje dla kobiecej postaci (CLka) - Znalazlas
;  * Dobieranie pojedynczego nozyka ze skladu
;  * Dobieranie tylko odpowiedniej ilosci ziol do Corpa

; Krzewy
; OIJ - Krzew Mandragory
; HJJ - Krzew ZenShenia
; ZIJ - Krzew Czosnku
; TIJ - ??? [Krzew Krwawnika?]

; Ziola
set %zioloTyp1 RZF ; sieci pajecze
set %zioloNazwa1 Sieci pajecze

set %zioloTyp2 KZF ; czosnek
set %zioloNazwa2 Czosnek

set %zioloTyp3 JZF ; zenshen
set %zioloNazwa3 ZenShen

set %zioloTyp4 MZF ; mandragora
set %zioloNazwa4 Mandragora

set %zioloTyp5 KUF ; perly
set %zioloNazwa5 Perly

set %zioloTyp6 WZF ; belladona
set %zioloNazwa6 Belladona

set %zioloTablicaRozmiar 6

set %ziolaDoSuszenia TIJ_OIJ_HJJ_ZIJ_
set %doOdlozenia KZF_JUF_MZF_JZF_TZF_VZF_NZF_HUF_YZF_XZF_

; ==== EDYCJA ====
; Zmienna %sklad ustawi siê automatycznie, bêdzie to pojemnik w którym przechowywane bêdš wykopane zio³a.
; Standardowo: %sklad to bezpieczny pojemnik domku a w nim znajduje siê %skladNarzedzia
set %skladNarzedzia YHBQPPE
set %dragPauza 1           ; Pauza przy drag/drop
set %dropPauza 1          ; Pauza przy drag/drop
set %ileZiol 6             ; Ilosc ziol jaka postac ma dobierac (Corp Por - perly/belka)
set %roznicaLeczenie 60    ; Roznica w HP przy ktorej ma leczyc IVM
; ==== Koniec EDYCJI ====

set %timeout 20
set #sysmsgcol 154
gosub r_menu
pokaz_menu:
set #menubutton n/a
menu show
repeat
until #menubutton <> n/a
if #menubutton = closed
halt
else
goto #menubutton

start:

 event macro 34 0
 event macro 31 0
 gosub otworzPojemnik #BACKPACKID
 gosub otworzPojemnik #CHARID

 menu get tryb_select
 set *zielarka_tryb #menures
 menu get suszenie_check
 set *zielarka_suszenie #menures
 menu get odkladanie_check
 set *zielarka_odkladanie #menures
 if *zielarka_suszenie = #true
 {
  finditem TXL_PMF_RMF_SMF_QVJ_FWJ G_2
  if #findid = x || #finddist > 2
  {
   event sysmessage Musisz miec brazier w zasiegu przy starcie skryptu!
   goto pokaz_menu
  }
  set %brazier #findid
 }
 if *zielarka_odkladanie = #true
 {
  finditem BUD_ZTD_UYD_GZD_CZD_OZD_IIF_HIF_KIF_JIF_IKF_RLL_CML_DHP_YGP_XGP_AHP_ZGP_KHP_JHP_MHP_LHP_GHP_CUD_KUD_ZJF_KKF G_2
  if #findid = x || #finddist > 2
  {
   event sysmessage Musisz miec pojemnik w zasiegu przy starcie skryptu!
   goto pokaz_menu
  }
  set %pojemnik #findid
  set %sklad #FINDID
 }

 set %pozycja_startowa_x #charposx
 set %pozycja_startowa_y #charposy

 menu hide
 gosub zmiana_miejsca

goto loop

sub odlozNozyk
 event sysmessage Odkladam nozyk do glownego plecaka.
 finditem WSF C_ , #CHARID
 if #FINDCNT > 0
 {
  exevent drag #FindID
  wait %dragPauza
  exevent dropc #BACKPACKID
  repeat
   wait 1
   finditem WSF C_ , #CHARID
  until #FINDCNT = 0
 }
return

; =======
;  Zabij
; =======
zabij:
 gosub odlozNozyk
 if #enemyid <> N/A
 {
  goto czaruj
 }
return

; ========
;  Czaruj
; ========
czaruj:
 set #ltargetid #enemyid
 set #ltargetkind 1
 event macro 15 41
 if #targcurs <> 1
 {
  wait 2
  goto czaruj
 }
 event macro 22 0
 wait 10
 if #enemyid <> N/A
 {
  goto czaruj
 }
return

; ======
;  LOOP
; ======
loop:

 if #targcurs = 1
 key esc

 if *zielarka_tryb = 1
 {
  finditem %narzedzie C_ , #CHARID
  if #findkind = -1
   {
    event sysmessage Nie znalazlem narzedzia w rece.
    gosub ustaw_narzedzie
   }
 }
 if *zielarka_tryb = 3
 {
  finditem %narzedzie C_ , #BACKPACKID
  if #findkind = -1
  {
   event sysmessage Nie znalazlem narzedzia w glownym plecaku postaci.
   gosub ustaw_narzedzie
  }
 }

 set #lobjectid %narzedzie
 set %_JIndex #JIndex
 event macro 17 0
 target 2s
 gosub ustawLastTarget
 set #ltargetkind 2
 event macro 22 0
 gosub ScanJournal %_JIndex Nie_udalo_Ci_sie_nic_znalezc Nie_udalo_Ci_sie_niczego_znalezc Nie_udalo_Ci_sie_zebrac_nic_pozytecznego Nie_dostrzegasz_tu_nic_nadajacego_sie_do_uzytku Zakonczono_prace Znalazles_jakies_ziolo Znalazlas_jakies_ziolo Twoje_narzedzie_do_niczego
 if #result = 2 || #result = 3 || #result = 4 || #result = 6 || #result = 7 || #result = 8
 wait 1
 if #result = 5
 {
  if %kop <> 24
   set %kop %kop + 1
  else
  {
   set %miejsce %miejsce + 1
   ; if %miejsce > *zielarka_miejsca
   ; {
   ; if *zielarka_suszenie = #true
   ; gosub suszenie
   ; if *zielarka_odkladanie = #true
   ; gosub odkladanie
   ;}
   gosub zmiana_miejsca
   set %kop 0
  }
 }
 
 if #ENEMYID <> N/A
 {
  goto zabij
 }
 
 if C in #CHARSTATUS
 {
  gosub LeczenieOdtruj
 }
 
 if #HITS < #MAXHITS - %roznicaLeczenie
 {
  gosub LeczenieIVM
 }

goto loop

; ================
;  Zmiana miejsca
; ================
sub zmiana_miejsca

 if *zielarka_suszenie = #true
 {
  gosub idzPozycjaStartowa
  gosub suszenie
 }
 if *zielarka_odkladanie = #true
 {
  gosub idzPozycjaStartowa
  gosub odkladanie
 }

 if %miejsce = 0 || %miejsce = n/a || %miejsce > *zielarka_miejsca
 set %miejsce 1
 for %i 1 *zielarka_miejsca
 {
  if %miejsce = %i
  {
  repeat
   event pathfind *xpos . %i *ypos . %i
   wait 1s
  until #charposx = *xpos . %i && #charposy = *ypos . %i
 break
  }
 }
return

; ===========
;  Suszenie
; ===========
sub suszenie
 event sysmessage Suszenie.
 if #charposx <> %pozycja_startowa_x || #charposy <> %pozycja_startowa_y
  gosub idzPozycjaStartowa
 repeat
  finditem %ziolaDoSuszenia C_ , #BACKPACKID
  if #FINDCNT > 0
  {
   set %iloscKupek #FINDCNT
   set #lobjectid #findid
   set #ltargetid %brazier
   set #ltargetkind 1
   event macro 17 0
   target 5s
   event macro 22 0
   repeat
    wait 1
    finditem %ziolaDoSuszenia C_ , #BACKPACKID
   until #FINDCNT < %iloscKupek
  }
 until #FINDCNT = 0
return

; ==============================================
;  Odk³adanie + Uzupe³nianie rzeczy (ziola itd)
; ==============================================
sub odkladanie
 event sysmessage Odkladanie.
 gosub idzPozycjaStartowa
 repeat
  finditem %doOdlozenia C_ , #BACKPACKID
  if #FINDCNT > 0
  {
   gosub przeniesPrzedmiot #findid %pojemnik #FINDSTACK
   finditem %doOdlozenia C_ , #BACKPACKID
  }
 until #FINDCNT = 0

 ; Uzupe³nianie ziol w glownym plecaku

 for %i 1 %zioloTablicaRozmiar
 {
  finditem %zioloTyp . %i C_ , #BACKPACKID
  if #findstack < %ileZiol
  {
   set %ilePrzeniesc %ileZiol - #FINDSTACK
   event sysmessage Uzupelniam %zioloNazwa . %i : %ilePrzeniesc
   gosub otworzPojemnik %sklad
   finditem %zioloTyp . %i C_ , %sklad
   gosub przeniesPrzedmiot #findid #BACKPACKID %ilePrzeniesc
  }
 }

return

; =================
;  Ustaw narzêdzie
; =================
sub ustaw_narzedzie
 event sysmessage Ustawiam narzedzie.
 if *zielarka_tryb = 1
 {
  set %narzedzieTyp WSF
  set %narzedzieNazwa Nozyk zielarza
 }
 if *zielarka_tryb = 3
 {
  set %narzedzieTyp TBG
  set %narzedzieNazwa Lopatka zielarza
 }

 ; Sprawdzanie narzedzia w rece
 finditem %narzedzieTyp C_ , #CHARID
  if #findkind = -1
  {
   ; Sprawdzanie narzedzia w glownym plecaku postaci
   finditem %narzedzieTyp C_ , #BACKPACKID
   if #findkind = -1
   {
    event sysmessage Nie znalazlem narzedzia w rece ani w plecaku postaci.
    gosub idzPozycjaStartowa
    gosub otworzPojemnik %sklad
    gosub otworzPojemnik %skladNarzedzia
    
    finditem %narzedzieTyp C_ , %skladNarzedzia
    if #findkind = -1
    {
     display Brakuje narzedzi: %narzedzieNazwa KONCZE
     halt
    }
   }
  }

  set %narzedzie #findid
  exevent drag %narzedzie
  wait %dragPauza
  exevent droppd
  wait %dropPauza
  
  gosub zmiana_miejsca

return

; ===============
;  Dodaj Miejsce
; ===============
miejsce_dodaj:
if *zielarka_miejsca = n/a
set *zielarka_miejsca 0
set *zielarka_miejsca *zielarka_miejsca + 1
set *xpos . *zielarka_miejsca #charposx
set *ypos . *zielarka_miejsca #charposy
event sysmessage Zapisano miejsce nr: *zielarka_miejsca
menu delete miejsca_txt
menu font bgcolor btnface
menu font color green
menu text miejsca_txt 165 125 *zielarka_miejsca
goto pokaz_menu

; =======================
;  Usun ostatnie miejsce
; =======================
miejsce_usun_ostatnie:
 if *zielarka_miejsca > 0
 {
  event sysmessage Usunieto ostatnie miejsce.
  set *zielarka_miejsca *zielarka_miejsca - 1
  menu delete miejsca_txt
  menu font bgcolor btnface
  if *zielarka_miejsca = 0
   menu font color red
  else
   menu font color green
  menu text miejsca_txt 165 125 *zielarka_miejsca
 }
 else
  event sysmessage Lista jest pusta.
goto pokaz_menu

; ========================
;  Usun wszystkie miejsca
; ========================
miejsce_usun_wszystkie:
 event sysmessage Usunieto wszystkie miejsca.
 set *zielarka_miejsca 0
 menu delete miejsca_txt
 menu font bgcolor btnface
 menu font color red
 menu text miejsca_txt 165 125 *zielarka_miejsca
goto pokaz_menu

; ================
;  Tworzenie Menu
; ================
sub r_menu
 menu Clear
 menu window title Zielarstwo by drm!
 menu window size 250 250
 menu shape ramka 5 10 240 80 3 5 1 black 2 0
 menu shape ramka 5 110 240 100 3 5 1 black 2 0
 menu font style b
 menu text narzedzie_txt 5 2 Ustawienia
 menu text narzedzie_txt 5 102 Miejsca - ziemia
 menu font style
 menu text narzedzie_txt 20 25 Tryb pracy:
 menu text suszenie_txt 27 60 Suszenie:
 if *zielarka_suszenie = n/a
  set *zielarka_suszenie #false
 if *zielarka_odkladanie = n/a
  set *zielarka_odkladanie #false
 menu check suszenie_check 95 59 18 20 *zielarka_suszenie
 menu text odkladanie_txt 130 60 Odkladanie:
 menu check odkladanie_check 210 59 18 20 *zielarka_odkladanie
 menu text ilosc_miejsc_txt 15 125 Ilosc zapisanych miejsc:
 if *zielarka_miejsca = n/a
 {
  menu font color red
  menu text miejsca_txt 165 125 0
 }
 else
 {
  menu font color green
  menu text miejsca_txt 165 125 *zielarka_miejsca
 }
 menu font color black
 menu button miejsce_dodaj 74 147 100 23 Dodaj miejsce
 menu button miejsce_usun_ostatnie 18 177 95 23 Usun ostatnie
 menu button miejsce_usun_wszystkie 123 177 107 23 Usun wszystkie
 menu button start 84 217 80 23 Start
 menu font bgcolor white
 menu combo create tryb_select 95 22 130
 menu combo add tryb_select Nozyk - ziemia
 menu combo add tryb_select Nozyk - drzewa
 menu combo add tryb_select Lopatka - ziemia
 menu combo select tryb_select 1
return

sub ScanJournal
 set %JIndex %1
 set %_timeout #SCNT
ScanLoop:
 set %x %timeout + %_timeout
 if #SCNT > %_timeout + %timeout
return TIMEOUT
if #JIndex > %JIndex
{
 set %JIndex %JIndex + 1
 ScanJournal %JIndex
 for %i 2 %0
 {
 if % . %i in #journal
return %i
 }
}
goto ScanLoop
return

; ==========================
;  Idz do Pozycji Startowej
; ==========================
sub idzPozycjaStartowa
event sysmessage Ide do pozycji startowej (sklad).
if #charposx <> %pozycja_startowa_x || #charposy <> %pozycja_startowa_y
{
 repeat
  event pathfind %pozycja_startowa_x %pozycja_startowa_y
  wait 1s
 until #charposx = %pozycja_startowa_x && #charposy = %pozycja_startowa_y
}
return

; ==========================
;  otworzPojemnik %pojemnikID
; ==========================
sub otworzPojemnik
 set #lobjectid %1
 event macro 17 0
 repeat
  wait 1
 until #CONTID = #LOBJECTID
return

; ====================
;   Ustaw Last Target
; ====================
sub ustawLastTarget
if %kop = 0 || %kop = n/a
{
set #ltargetx #charposx
set #ltargety #charposy
}
if %kop = 1
{
set #ltargetx #charposx
set #ltargety #charposy + 1
}
if %kop = 2
{
set #ltargetx #charposx - 1
set #ltargety #charposy
}
if %kop = 3
{
set #ltargetx #charposx
set #ltargety #charposy - 1
}
if %kop = 4
{
set #ltargetx #charposx + 1
set #ltargety #charposy
}
if %kop = 5
{
set #ltargetx #charposx + 1
set #ltargety #charposy + 1
}
if %kop = 6
{
set #ltargetx #charposx
set #ltargety #charposy + 2
}
if %kop = 7
{
set #ltargetx #charposx - 1
set #ltargety #charposy + 1
}
if %kop = 8
{
set #ltargetx #charposx + 1
set #ltargety #charposy
}
if %kop = 9
{
set #ltargetx #charposx - 1
set #ltargety #charposy - 1
}
if %kop = 10
{
set #ltargetx #charposx
set #ltargety #charposy - 2
}
if %kop = 11
{
set #ltargetx #charposx + 1
set #ltargety #charposy - 1
}
if %kop = 12
{
set #ltargetx #charposx + 2
set #ltargety #charposy
}
if %kop = 13
{
set #ltargetx #charposx + 2
set #ltargety #charposy + 1
}
if %kop = 14
{
set #ltargetx #charposx + 2
set #ltargety #charposy + 2
}
if %kop = 15
{
set #ltargetx #charposx + 1
set #ltargety #charposy + 2
}
if %kop = 16
{
set #ltargetx #charposx - 1
set #ltargety #charposy + 2
}
return

sub LeczenieOdtruj
 gosub odlozNozyk
 if #mana < 11
  event macro 1 0 .wypij duza mana
 event macro 15 10
 target 3s
 event macro 23 0
 wait 10
return

sub LeczenieIVM
 gosub odlozNozyk
 if #mana < 11
  event macro 1 0 .wypij duza mana
 event macro 15 28
 target 3s
 event macro 23 0
 wait 10
return

; gosub przeniesPrzedmiot %1 [przedmiot] %2 [lokalizacja docelowa] %3 [ile]
sub przeniesPrzedmiot
  exevent drag %1 %3
  wait %dragPause
  exevent dropc %2
  repeat
   wait %dropPause
   finditem %1 C
  until #FINDBAGID = %2 || #FINDKIND = -1
return
