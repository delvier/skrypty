                                ;=========================================
; Script Name: Krowa Tamer Plus 4.4
; Author: Based on Uncle Dave (mod by Krowa)
; Author: Krowa4.51.0
; Client Tested with: 4.0.9b
; EUO version tested with: 1.5.1.285
; Shard OSI / FS: OSI Tested Only
; Revision Date: 4/28/2015
; Public Release: 6/10/2004
set %wersja "4.4"

; 66.0 - Grizzly


; 0% - krowa, pies, kura
; 1% - kot
; 2% - juczny kon, juczna lama
; 3% - ptaki, papuga
; 8% - owca, koza, krolik
; 13% - ogromna ropucha, prosie
; 18% - szczur
; 23% - dziki kot, dziki pies, mouglin, bagienny mouglin
; 24% - mala zaba
; 25% - jelen
; 26% - kruk
; 28% - sniezny mouglin
; 29% - nietoperz mong
; 30% - jelen, wiewiorka, fretka
; 31% - wilk
; 33% - ogromny szczur, kon trykenski, mroczny mouglin, wiekszy mongbat
; 34% - zaba
; 35% - maciora
; 36% - mors
; 37% - ropucha
; 38% - niedzwiedz brunatny
; 39% - gremlin, waz
; 40% - ogromny skorpion, kon, morgan, gelderlander, szary waz
; 41% - lania, ameba
; 42% - pajak
; 43% - wielka zaba, kon pustynny
; 44% - szary wilk
; 45% - byk, niedzwiedz czarny
; 48% - lesny ostard, dziobak
; 49% - goryl
; 51% - wsciekly wilk, lodowy waz
; 53% - plomienny jaszczur, sniezny ostard
; 54% - olbrzymi pajak
; 55% - byk
; 56% - olbrzymi skorpion
; 57% - aligator, chochlik
; 58% - orzel, krokodyl, mustang, montrun, mroczny kagok, armok1
; 59% - skalny jaszczur
; 60% - pustynny ostard, ogromny waz, olbrzymi skalny pajak, shadeon, taigok, chigok, kirin, lesny pelzacz, wlochaty pajeczak, widmowy chochlik, skalny pelzacz
; 61% - arktyczny wilk
; 62% - niedzwiedz polarny, pantera, olbrzymi skalny skorpion, samiec beta, pustynny lis
; 63% - lodowa tarantula, dziki rzadki ostard, pegaz, mglisty wierzchowiec, mglista armana, shadeon, karmazynowy kagok, armok2
; 65% - szalony ostard, armok3, mglisty armok, czarna pantera
; 66% - imp, niedzwiedz grizzly
; 67% - lampart sniezny
; 68% - wsciekly wilk - Przywodca stada, jadowita ropucha
; 70% - kosciany wierzchowiec, jaszczur gorski, zuk krwiopijca
; 71% - olbrzymi zuk, olbrzymi skarabeusz, pelzacz skalny, piekielny ogar, mrozny wilk, bialy wilk
; 73% - lodowa anakonda, bagienna wyverna, anakonda szara, scierwojad
; 74% - pajak szablonogi
; 76% - wiekszy piekielny ogar
; 76.x - ksiezycowy armok
; 76.5 lub .6% - plomienna tarantula
; 77% - smok bagienny, wargh
; 78% - drake, lodowy drake, morski drake, bagienny drake
; 80% - lodowa wyverna, wyverna, plomienna wyverna
; 81% - pancernik
; 82% - opancerzony smok bagienny, koszmar, tarantula
; 83% - olbrzymia tarantula, pajak wampir, wodny ostard
; 85% - prastara istota
; 86% - lodowy smok
; 88% - smok
; 91% - jednorozec
; 92% - skalny smok
; 93% - ogar zgnilizny

initevents

event sysmessage Czyszczenie listy ignorowanych.
ignoreitem reset
ignoreitem #CHARID


set !kolorMenuOswajam green
set !kolorMenuIdle    $E0E0E0

set %followers 7
set %tamming_odleglos 10
set %tamming_odleglos_czekaj_po_prowokacji 8s           ; Ile czekac po sprowokowaniu stworzenia ktore bylo dalej niz %tamming_odleglosc
set %sound D:\games\uo\easyuo\tada.wav

set %leczRoznica 10

;set %KsiegaRun1 WIIFNQE   ; Yew Orly
set %KsiegaRun1 PRGTUQE   ; Destard

set %stajniaKsiegaID SBFZOQE
set %stajniaRuna 16

set %runaStart 1
set %runaKoniec 16

; Ilosc ziol przy restocku.
SET !perlyRestock       111
SET !krwawnikRestock    111
SET !czosnekRestock     222
SET !zenshenRestock     222
SET !mandragoraRestock  288
SET !belladonaRestock   111
SET !sieciRestock       222
SET !siarkaRestock      222

set %runaAktualna %runaStart - 1
set %runaNastepna %runaStart - 1
set %ksiegaAktualna %KsiegaRun1
set %zwierzetaOdstawiam XE

SET %zloto POF_
SET %kamienie 	HVF_UVF_FVF_EVF_OVF_VUF_GVF_RVF_BVF_VVF_NVF_ZVF_EVF_HVF_BVF_RVF_ZVF_GVF_NVF_UVF_FXS_GXS_ZWS_AXS_TWS_WWS_VWS_UWS_VVF_CWL_LWL_UJG_IJG
SET %ziola KUF_JUF_KZF_JZF_MZF_WZF_RZF_SZF_DUF_TZF_UZF_YZF_IUF_
SET %scrolls AUL_BUL_cul_dul_EUL_FUL_GUL_hul_iul_JUL_KUL_LUL_MUL_nul_oul_PUL_QUL_RUL_SUL_tul_UUL_vul_wul_XUL_YUL_zul_avl_gvl_fvl_ivl_hvl_cvl_NTL_otl_ptl_qtl_rtl_stl_ttl_utl_VTL_wtl_XTL_YTL_ZTL_qxl_pxl_zxl_nxl_wxl_vxl_yxl_xxl_zfj_byl_cyl_dyl_eyl_fyl_gyl_hyl_iyl_jyl_kyl_kym_pym_sym_tym_wym_uym_
SET %arrowsbolts RWF_LNK_FKF_
SET %solenloot GMF_OKF_TTO_IJG_
SET %jukaloot JSL_USL_WOH_YWL_
SET %paragonloot IIF_IKF_BUD_HIF_
;Resource loot lists
SET %feather VLK

gosub InitMenu
gosub InitAnimalTaming
gosub InitPickupRegs

loop:
 if #MENUBUTTON = CLOSED
 {
  event exmsg #CHARID 3 30 Menu zamkniete. Makro zatrzymane!
  halt
 }
 menu window color !kolorMenuIdle
 gosub Dispatch
goto loop

sub InitMenu
menu clear
menu window title Krowa Tamer Plus %wersja
menu window size 320 460
menu window color !kolorMenuIdle
menu font bgcolor $E0E0E0
menu font name MS Sans Serif
menu font size 8
menu font style b
menu text lblmovement 10 5 Features
menu font style n
menu check autotame 10 20 130 15 #TRUE AUTO Oswajanie
menu check autoatak 10 35 130 15 #TRUE Auto Prowokacja
menu font style b
menu text lblrelease 10 55 Jak uwalniac zwierzeta
menu font style n
menu check autobramy 10 70 130 15 #false Auto BRAMY
menu check zabijOswojonymi 10 85 130 15 #false ZABIJAJ oswojonymi
menu check zabijMagia 10 100 130 15 #false Zabijaj magi¹
menu font style b
menu text lblmisc 10 120 Poruszanie postacia
menu font style n
menu check dontmove 10 135 130 15 #true Nie ruszaj postaci
menu check returnstart 10 150 130 15 #false Powrot na poczatek
menu font style b
menu text lblstatistics 150 5 Statystyki
menu font style n
menu text lbltameskill 150 20 Animal Taming:
menu text lblloreskill 150 35 Animal Lore:
menu text lbltameid 150 50 Animal Id:
menu text lbltamecount 150 65 Aniaml Type:
menu text lbltamecount 150 80 Podejsc:
menu font align right
menu text tameskill 310 20 0/0
menu text loreskill 310 35 0/0
menu text tameid 310 50 N/A
menu text tametype 310 65 N/A
menu text tamecount 310 80 0
menu font align left

menu button manualtame 150 105 80 25 OSWAJAJ
menu button resetstart 150 130 80 25 Reset Start
menu button ignorecurrent 230 105 80 25 Ignoruj aktualne
menu button resetignore 230 130 80 25 Reset IGNOR
menu button otworzPojemnikKamienie 150 155 160 25 Pojemnik KAMIENIE

menu text lbl_przerwa_po_gate 10 185 Przerwa po gate w sekundach
menu Edit przerwa_po_gate 160 183 30 1
menu button idzStajnia 195 181 100 25 Idz STAJNIA
menu button sortujWPlecaku 195 211 100 25 Sortuj Plecak
menu button restockBank 195 235 100 25 Restock BANK

menu check odstawdostajni 10 165 130 15 #true Zwierzêta do stajni
menu check leczenie 10 205 130 15 #true Leczenie Magia

menu text lbl_aktualna_runa 10 226 Aktualna runa:
menu Edit aktualna_runa 90 223 25 0
menu Edit ostatnia_runa 120 223 25 %runaKoniec

menu text lbl_stajnia 10 246 Stajnia:
menu Edit stajniaKsiegaID 90 243 55 %stajniaKsiegaID
menu Edit stajniaNumerRuny 155 243 25 %stajniaRuna

menu check tnijciala 10 265 130 15 #true Tnij Ciala

; --------
; Status
; --------

menu shape bgstatus 0 438 320 17 3 7 1 $999999 7 $E0E0E0
menu font color $666666
menu text lblstatus 5 440 Status:
menu text status 40 440 Rozpoczynam

; --------
; Status
; --------

gosub MenuUpdateStats
menu show
menu HideEUO
set #menubutton N/A
return

sub InitAnimalTaming
event macro 8 2 ; open status
wait 1s
set %statusposx #contposx
set %statusposy #contposy
set %charstartx #charposx
set %charstarty #charposy
set %charstartz #charposz
set %tameskill -1
set %tameigtimer 0
set %tamecount 0
set %tameid N/A
return

sub InitPickupRegs
set %reagents KUF_JUF_MZF_JZF_KZF_WZF_RZF_SZF
event macro 8 7 ; open backpack
wait 1s
return

sub Dispatch

 onhotkey F1
  menu set autotame #FALSE
 onhotkey F1 ALT
  menu set autotame #TRUE

 gosub szukajSmoczejZjawy

 menu set status Idle
 gosub ScriptIdletime
 gosub TamingCheckSkill
 set %finditems N/A
 menu get autotame
 if #menures = #true
 {
  gosub TamingIgnore reset
	set %finditems %tameable
 }

 gosub FindClosestItem %finditems
 if %finddist = N/A
 {
  menu get returnstart
	if #menures = #true
	{
		gosub PathfindWait %charstartx %charstarty %charstartz
		if %moveres = error
		menu set returnstart #false
	}

	; ----------------------------------------------------
	; RuneBook
	; ----------------------------------------------------

	menu get autobramy
	if #menures = #true
	{
    ; Aktualna runa z Menu
    menu get aktualna_runa
    set %runaAktualna #MENURES
    set %runaNastepna %runaAktualna + 1

		if %runaNastepna > %runaKoniec
			set %runaNastepna %runaStart
		menu get przerwa_po_gate
		if #menures > 0
			set %przerwa_po_gate_sekundy #menures , s
		else
			set %przerwa_po_gate_sekundy 60 , s

		event sysmessage Dajemy mobkom czas na respown. Czekam %przerwa_po_gate_sekundy
		wait %przerwa_po_gate_sekundy
		event sysmessage Przenosze do miejsca: %runaNastepna

    ; Jezeli odstawiamy oswojone mobki do stajni to stawiana jest brama do stajnegoo, mobki sprzedawane/przechowywane a nastepnie
    ; kolejna pozycja z runebooka.
		menu get odstawdostajni
		if #menures = #true
		{
     ignoreitem reset
		 set %zwierzeta 0
		 finditem #CHARID
     set %mojaReputacja #FINDREP
		 finditem * G_7
		 for %a 1 #findcnt
			{
				set #findindex %a
				if #findrep = %mojaReputacja && #findid <> #charid
					set %zwierzeta %zwierzeta + 1
			}
			if %zwierzeta > 2
				gosub OdstawDoStajni
		}

    ; Gate do nastepnej lokalizacji
    call libs\runebookCL.lib.euo runebook %KsiegaRun1 %runaNastepna
;		gosub UzyjRunebooka
		set %runaAktualna %runaNastepna
 		menu set aktualna_runa %runaAktualna

    ; Jezeli w docelowej lokalizacji ma byc odpalony inny skrypt to dzieje sie to tutaj
    call taming\ , %KsiegaRun1 , _ , %runaAktualna , .euo
		; Resetujemy mobki to tamowania.
		ignoreitem reset

	}

	; ----------------------------------------------------
	; RuneBook
	; ----------------------------------------------------
 }
 if %finddist <> N/A
 {
	if %findtype in %tameable
   gosub Taming %findid
	if %findtype in %reagents
   gosub PickupRegs %findid
 }
return

sub ScriptIdletime
 if #menubutton = manualtame 2
 {
  event sysmessage Reczne oswajanie.
  set #menubutton N/A
  gosub TamingManual
 }
 if #menubutton = resetignore 2
 {
  event sysmessage Resetowanie ignorowanych.
  set #menubutton N/A
  ignoreitem reset
 }
 if #menubutton = resetstart 4
 {
  event sysmessage Restowanie pozycji startowej.
  set #menubutton N/A
  set %charstartx #charposx
  set %charstarty #charposy
  set %charstartz #charposz
 }

 if #menubutton = otworzPojemnikKamienie 2
 {
  event sysmessage Otwieranie pojemnika na kamienie.
  set #menubutton N/A
  gosub OtworzPojemnikNaKamienie
  gosub MenuUpdateStats
 }

 if #menubutton = idzStajnia
 {
  event sysmessage Idz do stajni.
  set #menubutton N/A
  gosub OdstawDoStajni
  gosub MenuUpdateStats
 }

 if #menubutton = sortujWPlecaku
 {
  event sysmessage Sortowanie rzeczy w plecaku.
  set #menubutton N/A
  gosub sortujWPlecaku
 }

 if #menubutton = restockBank
 {
  event sysmessage Restock w Banku.
  set #menubutton N/A
  call libs\restockBank.euo
 }

 menu get tnijciala
 if #menures = #true
 {
  finditem YFM G_2
  if #FINDCNT > 0
   call libs\tnijciala.euo
 }

return

sub MenuUpdateStats
 chooseskill anim
 str right #skill 1
 set %_skilldec #strres
 set %_skill #skill / 10
 set %_skillcap #skillcap / 10
 menu set tameskill %_skill , #dot , %_skilldec , / , %_skillcap

 chooseskill anil
 str right #skill 1
 set %_skilldec #strres
 set %_skill #skill / 10
 set %_skillcap #skillcap / 10
 menu set loreskill %_skill , #dot , %_skilldec , / , %_skillcap
 menu set tameid %tameid
 menu set tametype %tametype
 menu set tamecount %tamecount
return

sub Taming

 finditem %1
 if #findkind = -1
 return
 menu window color !kolorMenuOswajam
 set %tameid #findid
 set %tametype #findtype
 set %tamedist #finddist
 if %tameid <> #ltargetid
 event exmsg %tameid 3 30 Wybierz mnie!
 ; Auto Atak
 menu get leczenie
 if #menures = #true
  gosub Leczenie

 gosub AutoAttack
 set %tamecount 0
 gosub MenuUpdateStats
 gosub TameAnimal

 set %tameid N/A

return

sub TamingManual
 if %tameid <> N/A
 {
  event sysmessage Juz cos oswajasz.
  return
 }
 menu set status Czekam na cel
 gosub GetTargetId
 gosub Taming #ltargetid
return

sub TamingCheckSkill
 menu get autotame
 if #menures = #false
  return
 chooseskill anim
 if #skill = #skillcap
 {
	display ok Stalo sie cos niemozliwego. 100 Animal Tamingu
	menu set autotame #false
	return
 }
 if %tameskill = #skill
return

set %tameskill #skill
; ---------------------------------------------------
; this chart was auto-generated using TamingChart.xls
; ---------------------------------------------------
if %tameskill >= 0 && %tameskill < 40
set %tameable HG_RD_NF_BE_NF_GO_WC
if %tameskill >= 40 && %tameskill < 160
set %tameable HG_RD_NF_BE_NF_GO_WC_RF_AG_KB_WG_WG
if %tameskill >= 160 && %tameskill < 220
set %tameable HG_RD_NF_BE_NF_GO_WC_RF_AG_KB_WG_WG_IG_NG_ZF_JF_TF_PF
if %tameskill >= 220 && %tameskill < 280
set %tameable HG_RD_NF_BE_NF_GO_WC_RF_AG_KB_WG_WG_IG_NG_ZF_JF_TF_PF_TC
if %tameskill >= 280 && %tameskill < 340
set %tameable GO_WC_RF_AG_KB_WG_WG_IG_NG_ZF_JF_TF_PF_TC_BB_TG_VE_PG
if %tameskill >= 340 && %tameskill < 400
set %tameable WC_RF_AG_KB_WG_WG_IG_NG_ZF_JF_TF_PF_TC_BB_TG_VE_PG_CO_CG_JG_ZGB_XF_SF_MG_SG_OF_BO_WN_EG_QC
if %tameskill >= 400 && %tameskill < 460
set %tameable RF_AG_KB_WG_WG_IG_NG_ZF_JF_TF_PF_TC_BB_TG_VE_PG_CO_CG_JG_ZGB_XF_SF_MG_SG_OF_BO_WN_EG_QC_BG_EG_VF_DG
if %tameskill >= 460 && %tameskill < 520
set %tameable IG_NG_ZF_JF_TF_PF_TC_BB_TG_VE_PG_CO_CG_JG_ZGB_XF_SF_MG_SG_OF_BO_WN_EG_QC_BG_EG_VF_DG_ZI_ZE_IH
if %tameskill >= 520 && %tameskill < 580
set %tameable IG_NG_ZF_JF_TF_PF_TC_BB_TG_VE_PG_CO_CG_JG_ZGB_XF_SF_MG_SG_OF_BO_WN_EG_QC_BG_EG_VF_DG_ZI_ZE_IH_UF_UE
if %tameskill >= 580 && %tameskill < 640
set %tameable TF_IG_TC_BB_TG_VE_PG_CO_CG_JG_ZGB_XF_SF_MG_SG_OF_BO_WN_EG_QC_BG_EG_VF_DG_ZI_ZE_IH_UF_UE_VD_YF_L_M
if %tameskill >= 640 && %tameskill < 700
set %tameable W_FN_JD_KG_PF_TF_IG_NG_YG_XG_CB_UF_TC_BB_TG_VE_PG_CO_CG_JG_ZGB_XF_SF_MG_SG_OF_BO_WN_EG_QC_BG_EG_VF_DG_ZI_ZE_IH_UF_UE_VD_YF_L_M_SD_AH_WF_QE
if %tameskill >= 700 && %tameskill < 730
set %tameable W_PG_FN_QF_TG_JD_KG_YG_XG_CB_TC_CO_CG_JG_ZGB_XF_SF_MG_SG_OF_BO_WN_EG_QC_BG_EG_VF_DG_ZI_ZE_IH_UF_UE_VD_YF_L_M_SD_AH_WF_QE_GE_ZD
if %tameskill >= 730 && %tameskill < 740
set %tameable AF_W_PG_FN_QF_TG_JD_KG_YG_XG_CB_TC_CO_CG_JG_ZGB_XF_SF_MG_SG_OF_BO_WN_EG_QC_BG_EG_VF_DG_ZI_ZE_IH_UF_UE_VD_YF_L_M_SD_AH_WF_QE_GE_ZD_OH
if %tameskill >= 740 && %tameskill < 760
set %tameable HD_AF_W_PG_FN_QF_TG_JD_KG_YG_XG_CB_TC_CO_CG_JG_ZGB_XF_SF_MG_SG_OF_BO_WN_EG_QC_BG_EG_VF_DG_ZI_ZE_IH_UF_UE_VD_YF_L_M_SD_AH_WF_QE_GE_ZD_OH
if %tameskill >= 760 && %tameskill < 780
set %tameable OF_JG_HD_AF_W_PG_FN_QF_TG_JD_KG_CB_TC_BG_EG_VF_DG_ZI_ZE_IH_UF_UE_VD_YF_L_M_SD_AH_WF_QE_GE_ZD_OH_YG_XG_RF
if %tameskill >= 780 && %tameskill < 796
set %tameable AF_AH_BG_CB_DG_EG_FN_GE_HD_IH_JD_JG_KG_L_M_MHB_OF_OH_PG_QE_QF_RF_SD_TC_TG_UE_UF_VD_VF_W_WF_XE_XG_YE_YF_YG_ZD_ZE_ZI_
if %tameskill >= 796 && %tameskill < 820
set %tameable AF_AH_BG_BK_CB_DG_EG_FN_GE_HD_IH_JD_JG_KD_KG_L_M_MG_MHB_OF_OH_PG_QE_QF_RF_SD_TC_TG_UE_UF_VD_VF_W_WF_XE_XG_YE_YF_YG_ZD_ZE_ZI_
; YE_XE_ - DRAK
; TF_ - Prosiaki
; PF_ - Owce
; JD_ - Ogromny waz
; QF_ - Jaszczor gorski
; AF_ - Wyverna
; NG_ - Krowa
; HD_ - Pajak szablonogi  / Tarantula
; IH_ - Lesny pelzacz
; BK_ - Dziobak
; MG_ - Mgliste wierzchowce
; UF_ - Plomienny jaszczur
; TC_ - Orze³
; CB_ - Ogromna ropucha
; KG_ - Armok / szalony ostard
; HH_ - Pustynny Gryf
; XX_ - Byk
; OF_ - kon / pegaz
; W_  - Imp
if %tameskill >= 820
set %tameable AF_AH_BG_BK_CB_CD_CG_CH_DF_DG_EG_EH_FN_GE_HD_HH_HHB_IH_JD_JJ_KD_KG_L_LD_M_MG_MHB_NC_OF_OH_PG_QE_QF_RF_SC_SD_SH_TC_TE_TJ_UB_UE_UF_VD_VF_W_WF_WJ_XE_XG_YE_YF_YG_ZD_ZE_ZI_

 gosub TamingIgnore gain
return

sub TamingIgnore
 if %1 = perm
 {
	ignoreitem %2
	return
 }
 if %1 = temp
 {
	ignoreitem %2 1
	set %tameigtimer #systime + 60000    ; Oryginalnie bylo 60000 [60s]
	return
 }
 if %1 = reset && #systime > %tameigtimer
 {
	ignoreitem reset 1
	return
 }
 if %1 = skill
 {
	ignoreitem %2 2
	return
 }
 if %1 = gain
 {
  ignoreitem reset 2
	return
 }
return

sub TameAnimal
 event sysmessage Zaczynam oswajac
 TamingLoop:
  menu get leczenie
  if #menures = #true
   gosub Leczenie

  finditem %tameid
  gosub szukajSmoczejZjawy
  if #findkind = -1
   return
  if #findrep = 1 || #findrep = 2 || #FINDREP = 0 ; 0 - paragon |  1  - blue  | 2 - green
  {
   gosub TamingIgnore perm %tameid
   return
  }

  event exmsg %tameid 3 30 Oswajam!
  menu window color !kolorMenuOswajam

  if #finddist > %tamming_odleglos
  {
   ; Tutaj czekamy jezeli postac ma sie nie ruszac
   menu get dontmove
   if #menures = #true
   {
    gosub AutoAttack
    menu set status Zwierze jest zbyt daleko! Czekam... %tamming_odleglos_czekaj_po_prowokacji
    wait %tamming_odleglos_czekaj_po_prowokacji
   }
   ; Inaczej podchodzimy do stworzenia
   else
   {
    event sysmessage Podchodzimy do zwierzaka
    gosub PathfindWait #findx #findy #findz
    if %moveres = error
    {
     gosub TamingIgnore temp %tameid
     return
    }
    gosub AutoAttack
   }

  finditem %tameid
  ; Jezeli nadal, mimo proby podejscia do stworzenia / prowokacji jest ono zbyt daleko ignorujemy je tymczasowo
  if #finddist > %tamming_odleglos
  {
   event exmsg %tameid 3 30 Zwierze jest zbyt daleko! Dystans: #finddist
   gosub TamingIgnore temp %tameid
   return
  }

   finditem %tameid
   if #findkind = -1
    return

   if #menubutton = ignorecurrent
   {
		set #menubutton N/A
		gosub TamingIgnore perm %tameid
		return
   }

   gosub ScriptIdletime
   goto TamingLoop
  }
 set %tamecount %tamecount + 1
 set %tamejournal #jindex + 1
 set %tamefollow #followers
 set #ltargetid %tameid
 set #ltargetkind 1
 event macro 13 35 ; animal taming
 target 5s
 event macro 22 0 ; last target
 gosub AutoAttack
 gosub TamingMonitor
 if #result = success
 {
  gosub TamingRelease
  gosub TamingIgnore perm %tameid
  return
 }
 if #result = retry
 {
	gosub ScriptIdletime
	goto TamingLoop
 }
 if #result = retrylater
 {
	gosub TamingIgnore temp %tameid
	return
 }
 if #result = ignore
 {
	gosub TamingIgnore perm %tameid
	return
 }
 if #result = cancel
 {
	menu set autotame #false
	return
 }
 if #result = skill
 {
	gosub TamingIgnore skill %tameid
	return
}
 gosub ScriptIdletime
 goto TamingLoop
return

sub TamingMonitor
 set %movetimer 0
 set %tametimer #systime + 18000
 TamingMonitorLoop:
 if #jindex >= %tamejournal
 {
	scanjournal %tamejournal
	set %tamejournal %tamejournal + 1
	if Ktore_stworzenie_chcesz in #journal
	{
   menu set status Oswajam zwierze
  }
	if Nie_udalo_ci_sie_oswoic_tego in #journal
	{
   return retry
  }
	if Udalo_ci_sie_oswoic in #journal
	{
   event macro 1 0 Witaj. Podazajcie za mna.
   if Twoje_zwierze_zachowuje_sie_niespokojnie in #journal
	 {
		event sysmessage Zwierze zachowuje sie niespokojnie
		menu get odstawdostajni
		if #menures = #true
		{
	   gosub OdstawDoStajni
		}
		else
		{
     menu get zabijOswojonymi
     if #menures = #true
     {
      menu set status Zabijam stworzenie innymi oswojonymi.
      gosub zabijOswojonymiStworzeniami
     }

     menu get zabijMagia
     if #menures = #true
     {
      menu set status Zabijam stworzenie magia.
      gosub TamingMageryKill
     }

     return

		}
	 }
	 return success
  }
	if Zdenerwowal in #journal
	{
   return retry
  }
	if Jestes_za_daleko in #journal
	{
   return retry
  }
  if Nie_mozesz_sie_poruszyc in #journal ; Sieci, paraliz
	{
   return retry
  }
  if Udalo_Ci_sie_wyswobodzic in #journal ; Paraliz z sieci - koniec
	{
   return retry
  }
	if You_must_wait_to_perform in #journal
  {
   return retry
  }
	if clear_path in #journal
	{
   return retry
  }
	if too_many_followers in #journal
	{
   return cancel
  }
	if That_is_not_within_your in #journal
	{
   event exmsg #CHARID 3 30 Nie widze zwierzaka!
   return retrylater
  }
  if Zwierze_zbyt_mocno_sie in #journal
  {
   return retrylater
  }
  if distracted in #journal
  {
   return retrylater
  }
  if someone_else in #journal
	{
   return retrylater
  }
	if tame_already in #journal
	{
   return ignore
  }
  if Nie_mozesz_go_oswoic in #journal
	{
   return ignore
  }
  if To_zwierze_jest_juz_oswojone in #journal
	{
   return ignore
  }
  if Nie_mozesz_oswoic_tego in #journal
	{
   return ignore
  }
  if Nie_masz_szans_na in #journal
	{
   event sysmessage Nie mam szans na oswojenie.
   return skill
  }
 }
 if #followers > %tamefollow
  return success
 if #menubutton = ignorecurrent
  return ignore
 if #systime > %tametimer
  return retry

 finditem %tameid
 if #findkind = -1
  return retry
 if #finddist > 3 && #systime > %movetimer
 {
	menu get dontmove
	if #menures = #false
	event pathfind #findx #findy #findz
	set %movetimer #systime + 1500
 }
 gosub ScriptIdletime
 goto TamingMonitorLoop
return

sub TamingRelease
 menu set status Pozbywanie sie zwierzeta.

 menu get zabijOswojonymi
 if #menures = #true
 {
  menu set status Zabijam stworzenie innymi oswojonymi.
  gosub zabijOswojonymiStworzeniami
 }

 menu get zabijMagia
 if #menures = #true
 {
  menu set status Zabijam stworzenie magia.
  gosub TamingMageryKill
 }

return

 menu set status Odstawiam zwierzeta do
 gosub GetAnimalDisplacement

return

sub zabijOswojonymiStworzeniami
 menu set status Zabijam zwierze.
 event exmsg %tameid 3 30 Zabijam!
 set #ltargetid %tameid
 set #ltargetkind 1
 event macro 1 0 Podazajcie za mna!
 event macro 1 0 Zabijcie!
 target 6s
 event macro 22 0 ; last target
 wait 1s
return

sub TamingMageryKill
 menu set status Zabijam zwierze.
 event exmsg %tameid 3 30 Zabijam!
TamingMageryKillLoop:
 finditem %tameid
 if #findkind = -1
  return
 if #mana < 40
 {
	chooseskill medi
	if #skill > 500
	{
		event macro 13 46 ; meditation
		wait 5s
		goto TamingMageryKillLoop
	}
	else
	{
		event sysmessage Musisz miec conajmniej 50 procent medytacji.
		menu set zabijMagia #false
		return
	}
 }
 set #ltargetid %tameid
 set #ltargetkind 1
 event macro 15 50 ; Flame Strike
 target 6s
 event macro 22 0 ; last target
 wait 1s
goto TamingMageryKillLoop
return

; GetYDisplacementForAnimType by Roadkill
; Mods by UncleDave
sub GetAnimalDisplacement
  set %screenydisp -32
 if %tametype in GE_ZD_L_M_TF__IG_NG__ZF_PF_EG_CO_TG_PG_VD
  set %screenydisp -25
 if %tametype in AH
  set %screenydisp -45
 if %tametype in DG_AG_WC_RF_WG_TC
  set %screenydisp -15
return

sub FindClosestItem
; finditem %1 G_16
 finditem %1 G_
 set %findid #findid
 set %findtype #findtype
 set %finddist #FINDDIST
 event exmsg #FINDID 3 30 Dystans: %finddist
 if #findcnt > 1
 {
	for %i 1 #findcnt
	{
;		finditem %1 %i G_16
		finditem %1 %i G_
		if #findkind <> -1 && #finddist < %finddist
		{
			set %findid #findid
			set %findtype #findtype
			set %finddist #finddist
		}
	}
 }
return

sub GetTargetId
 set #ltargetid N/A
 set #targcurs 1
 GetTargetIdLoop:
 if #targcurs = 1
  goto GetTargetIdLoop
return

sub PathfindWait
 set %moveres unknown
 menu get dontmove
 if #menures = #true
  return
 if #charposx = %1 && #charposy = %2
 {
	set %moveres success
	return
 }
 set %js #jindex
 event pathfind %1 %2 %3
 wait 10
 set %je #jindex
 for %ji %js %je
 {
  scanjournal %ji
	if pathfinding in #journal
	{
	 set %movex #charposx
	 set %movey #charposy
	 set %movetimer #systime + 500
   PathfindWaitLoop:
   if #systime < %movetimer
   goto PathfindWaitLoop
   if %movex = %1 && %movey = %2
   {
  	set %moveres success
    return
   }
   if %movex = #charposx && %movey = #charposy
   {
    set %moveres stopped
    return
   }
   set %movex #charposx
   set %movey #charposy
   set %movetimer #systime + 500
   goto PathfindWaitLoop
  }
	if can't_get_there in #journal
  {
   set %moveres error
   return
  }
 }
return

sub AutoAttack
 menu get autoatak
 if #menures = #true
 {
  menu set status Automatyczne prowokowanie stworzenia
  finditem %tameid
  if #findkind = -1
  {
   event sysmessage Nie znalazlem niczego do prowokowania.
   return
  }
  else
  {
   if #FINDREP = 0
    return #FALSE ; Paragon

   if #FINDREP = 4 || #FINDREP = 6 ; 4 - szary / 6 - red
   {
    event exmsg %tameid 3 30 Prowokuje!
    SET #LTARGETID %tameid
    EVENT macro 27 ; Attack
    wait 10
    gosub WarModeToggle
   }
  }
 }
return #true

sub GetContainerPos
finditem %1 C
if #findkind = -1
return #false
if #findtype = ZJF 5 ; backpack
set %contposx #findx + 22
set %contposy #findy + 16
set %contdropx #findx + 22
set %contdropy #findy + 12
return #true
if #findtype = CKF 5 ; bag
set %contposx #findx + 22
set %contposy #findy + 15
set %contdropx #findx + 21
set %contdropy #findy + 10
return #true
if #findtype = LKF 5 ; pouch
set %contposx #findx + 22
set %contposy #findy + 13
set %contdropx #findx + 25
set %contdropy #findy + 7
return #true
return #false


; Makro do obs³ugi Ksiegi Run - UO CLr
; Autor: Krowa
; Data: 25 kwietnia 2015

; Ksiêga run otwiera siê zawsze w tej samej pozycji.
; Konkretne pozycje w ksiêdze znajduj¹ sie na pozycjach:
; x  y
; 73 28 - Zmien nazwê ksiêgi

; X  Y
; 64 66  - 1   9  - 222 66
; 64 82  - 2   10 - 222 82
; 64 96  - 3   11 - 222 96
; 64 110 - 4   12 - 222 110
; 64 126 - 5   13 - 222 126
; 64 140 - 6   14 - 222 140
; 64 156 - 7   15 - 222 156
; 64 170 - 8   16 - 222 170

; Ksiêga run
; Typ: MPF
; Kolor: 108

; Moongate
; Typ: OTF
; Kolor: 88 - diament
; Kolor: 6  - gwiezdy szafir

; Pude³ko na kamienie
; Typ: LKF
; Color: 1195

; Kamienie
; Typ   Kolor
; FVF_ - 0 - szafir - popularniejszy
; IVF_ - 0 - szafir - drugi typ
; EVF_ - 0 - ametyst - rozpada sie
; MWF_ - 0 - diament
; VUF_ - 0 - gwiezdy szafir

sub UzyjRunebooka

if #mana < 70
{
	chooseskill medi
	event macro 13 46 ; meditation
	wait 10s
}

; Kamienie
set %kamienie FVF_IVF_MWF_VUF_EWF_DWF_TVF_
set %kamieniePojemnik #BACKPACKID

; Pierwsza strona
set %runaX1 64
set %runaY1 66
set %runaX2 64
set %runaY2 82
set %runaX3 64
set %runaY3 96
set %runaX4 64
set %runaY4 110
set %runaX5 64
set %runaY5 126
set %runaX6 64
set %runaY6 140
set %runaX7 64
set %runaY7 156
set %runaX8 64
set %runaY8 170

; Druga strona
set %runaX9 222
set %runaY9 66
set %runaX10 222
set %runaY10 82
set %runaX11 222
set %runaY11 96
set %runaX12 222
set %runaY12 110
set %runaX13 222
set %runaY13 126
set %runaX14 222
set %runaY14 140
set %runaX15 222
set %runaY15 156
set %runaX16 222
set %runaY16 170

set %KsiegaRunID %1
set %NumerRuny %2

event sysmessage %KsiegaRunID %NumerRuny

finditem %KsiegaRunID

if ( #FINDKIND = -1 )
{
	event sysmessage Nie znalazlem runebooka!
	return
}

; Sprawdzanie pojemnika na kamienie i kamieni.

finditem LKF C_ , #BACKPACKID

if #FINDCNT > 0
{
set %kamieniePojemnik #FINDID
}

call libs\pojemnik.euo otworz %kamieniePojemnik

finditem %kamienie C_ , %kamieniePojemnik ; Pojemnik na kamienie

if #FINDCNT > 0
{
set %kamienDoBramy #FINDID
}
else
{
event sysmessage Nie znalazlem kamieni. Nie ma jak postawic bramy.
return
}

set #LOBJECTID %KsiegaRunID
event macro 17
wait 10

; Je¿eli ksiêga jest otwarta wybieramy czar
; TODO: Sprawdzanie czy ksiêga jest otwarta (#CONTSIZE + #CONTPOS)
; #CONTPOSX: 0
; #CONTPOSY: 0
; #CONTSIZEX: 382
; #CONTSIZEY: 231
; #CONTKIND: GJZ

if #contSize = 382_231
{
		event sysmessage Runa: %NumerRuny Ksiega: %KsiegaRunID

		set %x %runaX . %NumerRuny
		set %y %runaY . %NumerRuny
		click %x %y

		set #LTARGETID %kamienDoBramy
		set #lTargetKind 1
		target 10s
		event macro 22 0 ; last target
}

					; Klejnot_rozpadl_sie_na

	finditem OTF ; Moongate

	while #FINDKIND = -1
	{
	finditem OTF
	}

	set %staraPozycjaX #CHARPOSX
	set %staraPozycjaY #CHARPOSY

	event macro 15 21 ; Teleport
	target 3s
	event macro 23 0 ; Target Self
	wait 2s

	; Odwo³anie bramy
	if %staraPozycjaX <> #CHARPOSX || %staraPozycjaY <> #CHARPOSY
	{
	event macro 15 33 ; Dispel Field
	target 5s
	event macro 23 0 ; Target Self
	}
	else
	{
	event sysmessage Postac nie przeniosla sie, lub brama postawiona jest w to samo miejsce.
	event macro 15 21 ; Teleport
	target 3s
	event macro 23 0 ; Target Self

	event sysmessage Odwoluje brame mimo wszystko.
	event macro 15 33 ; Dispel Field
	target 5s
	event macro 23 0 ; Target Self
	}

return

sub OtworzPojemnik

	if %2 = N/A || %3 = N/A
	{
	set %pozycjaPojemnikaX 0
	set %pozycjaPojemnikaY 150
	}
	else
	{
	set %pozycjaPojemnikaX %2
	set %pozycjaPojemnikaY %3
	}
	set %pojemnikID %1

	nextcpos %2 %3
	set #lobjectid %pojemnikID

	event macro 17 0
	wait 1s
	OtworzPojemnik:
	if #contid <> %pojemnikID
	{
	goto OtworzPojemnik
	}
	wait 1s

return

sub PrzechowajZwierzeta
 call libs\stajennyCL.euo
return

sub ZbierzRzeczy

set %widzeZiola 1
set %widzeKamienie 1
set %widzeZloto 1
set %widzeUstawione 1

while %widzeZiola = 1 || %widzeKamienie = 1 || %widzeZloto = 1 || %widzeUstawione = 1
{

; Zloto
FINDITEM POF_RZF_XUF G_3
IF #FINDCNT > 0
{
event macro 1 0 .grab zloto   ; POF
wait 20
}
else
	set %widzeZloto 0

; Kamienie
FINDITEM RZF_XUF G_3
IF #FINDCNT > 0
{
event macro 1 0 .grab kamienie
wait 20
}
else
	set %widzeKamienie 0

; Ziola
FINDITEM KUF_JUF_KZF_JZF_MZF_WZF_RZF_SZF_IUF_DUF_TZF_UZF_YZF_GUF_VZF_NZF_EAG_XZF_PZF_HUF G_3
IF #FINDCNT > 0
{
event macro 1 0 .grab ziola   ; RZF
wait 20
}
else
	set %widzeZiola 0

; Ustawione
; EEG - futra
; YLI - smocze udka
; GPF - mana
FINDITEM VLK_VRD_PUD_EEG_YLI_GPF G_3
IF #FINDCNT > 0
{
event macro 1 0 .grab ustawione
wait 20
}
else
	set %widzeUstawione 0
}

	return

sub OdstawDoStajni
 event macro 1 0 Podazajcie za mna. Wracamy!
 event exmsg #CHARID 3 30 Odstawiam zwierzeta do stajni!
 ; Do stajennego

 menu get stajniaKsiegaID
 set %stajniaKsiegaID #menures
 menu get stajniaNumerRuny
 set %stajniaRuna #menures

 call libs\runebookCL.lib.euo runebook %stajniaKsiegaID %stajniaRuna
 gosub PrzechowajZwierzeta

; gosub ZbierzRzeczy
; Powrót od stajennego w poprzednie miejsce
; gosub UzyjRunebooka %ksiegaAktualna %runaNastepna

return

; Do leczenie u wymannowania mobka - tak przynajmniej uwazaj¹ m¹drzy
sub Leczenie
 if #HITS < #MAXHITS - %leczRoznica
 {
  if #MANA < 30
  {
   if #ENEMYID = N/A
   {
    event macro 13 46 ; meditation
    wait 6s
   }
   else
   {
    gosub wypijMana
   }
	}
	else
	{
   gosub LeczenieIVM
   wait 10
  }
	if #MANA > 80
   gosub Lustro
 }
 if #HITS > 50 && #MANA > 22
 {
	 if #STR <= 100
		gosub BlessStr
	 if #INT <= 100
		gosub BlessInt
	 if C in #CHARSTATUS
		gosub LeczenieOdtruj
	}

return

sub LeczenieOdtruj
 event macro 15 24
 target 3s
 event macro 23 0
return

sub LeczenieIVM
  if #mana < 11
   gosub wypijMana
  if #mana >= 11
  {
	 event macro 15 28
   target 3s
   event macro 23 0
  }                                          `
return

sub Lustro
	event macro 15 35
	wait 3s
return

sub BlessStr
	event macro 15 15
	target 3s
	event macro 23 0
return

sub BlessInt
	event macro 15 9
	target 3s
	event macro 23 0
return

sub OtworzPojemnikNaKamienie
 finditem LKF C_ , #BACKPACKID
 for #FINDINDEX 1 #FINDCNT
 {
  call libs\pojemnik.euo otworz #FINDID
 }
return

sub WarModeOn
 repeat
  event macro 8 1 ; Open Paperdoll
  set !pixx #CONTPOSX + 215
  set !pixy #CONTPOSY + 215
  savePix !pixx !pixy  1
  if #PIXCOL = 14603990 || #PIXCOL = 16777207 ; PEACE
   EVENT macro 6 ; Toggle War Mode
  wait 1
 until #PIXCOL = 1052771 || #PIXCOL = 1577050   ; WAR
return

sub WarModeOff
 repeat
  event macro 8 1 ; Open Paperdoll
  set !pixx #CONTPOSX + 215
  set !pixy #CONTPOSY + 215
  savePix !pixx !pixy  1
  if #PIXCOL = 1052771 || #PIXCOL = 1577050   ; WAR
   EVENT macro 6 ; Toggle War Mode
  wait 1
 until #PIXCOL = 14603990 || #PIXCOL = 16777207 ; PEACE
return


sub WarModeToggle
 EVENT macro 6 ; Toggle War Mode
 wait 1s
 EVENT macro 6 ; Toggle War Mode
 wait 10
; gosub WarModeOn
; gosub WarModeOFF
return

sub wypijMana
 event macro 1 0 .wypij mala mana
 event macro 1 0 .wypij duza mana
return

; Sortuje ziola do pojemnika na ziola / Kamienie do pojemnika na kamienie
sub sortujWPlecaku

 finditem OKF C_ , #BACKPACKID
 if #FINDCNT > 0
 {
  set !pojemnikZiolaID #FINDID
 }

 finditem LKF C_ , #BACKPACKID
 if #FINDCNT > 0
 {
  set !pojemnikKamienieID #FINDID
 }

 set #LTARGETKIND 1
 ; Sortowanie ziol
 event macro 1 0 .sortuj ziola
 target 10s
 if !pojemnikZiolaID <> N/A
  set #LTARGETID !pojemnikZiolaID
 else
  set #LTARGETID #CHARID
 event macro 22 0 ; last target

 ; Sortowanie kamieni
 event macro 1 0 .sortuj kamienie
 target 10s
 if !pojemnikKamienieID <> N/A
  set #LTARGETID !pojemnikKamienieID
 else
  set #LTARGETID #CHARID
 event macro 22 0 ; last target

 event sysmessage Posortowane Kamienie i Ziola.

return

gosub szukajSmoczejZjawy
; Sprawdzamy czy znalezione stworzenie nie jest SMOCZA ZJAWA lub PARAGONEM! :)
 event sysmessage Szukam smoczej zjawy
 if #FINDTYPE = YE
 {
  if #FINDCOL = 17969 || #FINDCOL = 1792
  {
   set %tameid #FINDID
   event exmsg %tameid 3 30 SMOCZA ZJAWA!
   gosub zabijOswojonymiStworzeniami
   gosub TamingMageryKill
  }
 }
return
